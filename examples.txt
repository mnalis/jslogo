; FMSLogo compatibility initialisation
if primitivep "arc [
make "redefp "true
copydef ".arc "arc
define "__new_arc [[r d] [ if pendownp [rt 180 .arc :r :d lt 180]]]
copydef "arc "__new_arc
define "circle [[r] [__new_arc 360 :r]]
to __convert_rgb :arg
    if listp :arg [
        make "__r item 1 :arg
        make "__g item 2 :arg
        make "__b item 3 :arg
        make "__arg (word "rgb char 40 :__r ", :__g ", :__b char 41 )
    ]
    output :__arg
end
copydef ".setpc "setpc
define "__new_setpc [[boja] [.setpc __convert_rgb :boja]]
copydef "setpc "__new_setpc
copydef ".setsc "setsc
define "__new_setsc [[boja] [.setsc __convert_rgb :boja]]
copydef "setsc "__new_setsc
to setfc :color make "__floodcolor :color end
copydef ".fill "fill
to __new_fill
    make "__oldcolor pencolor
    setpc :__floodcolor
    .fill
    setpc :__oldcolor
end
copydef "fill "__new_fill
make "redefp "false
to distance :finish
    make "__start pos
    make "__x1 item 1 :__start
    make "__y1 item 2 :__start
    make "__x2 item 1 :finish
    make "__y2 item 2 :finish
    make "__dx :__x1 - :__x2
    make "__dy :__y2 - :__y1
    make "__distance sqrt :__dx ^ 2 + :__dy ^ 2
    output :__distance
end
;show text "arc
]


to star
  repeat 5 [ fd 100 rt 144 ]
end
clearscreen
star

to square :length
  repeat 4 [ fd :length rt 90 ]
end
to randomcolor
  setcolor pick [ red orange yellow green blue violet ]
end
clearscreen
repeat 36 [ randomcolor square random 200 rt 10 ]

clearscreen window hideturtle
repeat 144 [
  setlabelheight repcount
  penup
  fd repcount * repcount / 30
  label "Logo
  bk repcount * repcount / 30
  pendown
  rt 10
  wait 5
]
showturtle

to tree :size
   if :size < 5 [forward :size back :size stop]
   forward :size/3
   left 30 tree :size*2/3 right 30
   forward :size/6
   right 25 tree :size/2 left 25
   forward :size/3
   right 25 tree :size/2 left 25
   forward :size/6
   back :size
end
clearscreen
tree 150

to fern :size :sign
  if :size < 1 [ stop ]
  fd :size
  rt 70 * :sign fern :size * 0.5 :sign * -1 lt 70 * :sign
  fd :size
  lt 70 * :sign fern :size * 0.5 :sign rt 70 * :sign
  rt 7 * :sign fern :size - 1 :sign lt 7 * :sign
  bk :size * 2
end
window clearscreen pu bk 150 pd
fern 25 1
